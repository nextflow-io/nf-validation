/*
 * Copyright 2021-2022, Seqera Labs
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java"
    id "io.nextflow.nf-build-plugin" version "1.0.1"
}

ext.github_organization = 'nextflow-io'
ext.github_username = project.findProperty('github_username') ?: 'pditommaso'
ext.github_access_token = project.findProperty('github_access_token') ?: System.getenv('GITHUB_TOKEN')
ext.github_commit_email = project.findProperty('github_commit_email') ?: 'paolo.ditommaso@gmail.com'

jar.enabled = false

String computeSha512(File file) {
    if( !file.exists() )
        throw new GradleException("Missing file: $file -- cannot compute SHA-512")
    return org.apache.commons.codec.digest.DigestUtils.sha512Hex(file.bytes)
}

String now() {
    "${java.time.OffsetDateTime.now().format(java.time.format.DateTimeFormatter.ISO_DATE_TIME)}"
}

List<String> allPlugins() {
    return [new File(rootProject.rootDir, 'plugins/nf-validation')]
}

String metaFromManifest(String meta, File file) {
    def str = file.text
    def regex = ~/(?m)^$meta:\s*([\w-\.<>=]+)$/
    def m = regex.matcher(str)
    if( m.find() ) {
        def ver = m.group(1)
        //println "Set plugin '${file.parentFile.parentFile.parentFile.parentFile.name}' version=${ver}"
        return ver
    }
    throw new GradleException("Cannot find '$meta' for plugin: $file")
}

def timestamp = now()

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'io.nextflow.nf-build-plugin'
    
    repositories {
        mavenLocal()
        mavenCentral()
    }

    version = metaFromManifest('Plugin-Version',file('src/resources/META-INF/MANIFEST.MF'))

    tasks.withType(Jar) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
    
    /*
     * Creates plugin zip and json meta file in plugin `build/libs` directory
     */
    task makeZip(type: Jar) {
        into('classes') { with jar }
        into('lib') { from configurations.runtimeClasspath }
        manifest.from file('src/resources/META-INF/MANIFEST.MF')
        archiveExtension = 'zip'
        preserveFileTimestamps = false
        reproducibleFileOrder = true

        doLast {
            // create the meta file
            final zip = new File("$buildDir/libs/${project.name}-${project.version}.zip")
            final json = new File("$buildDir/libs/${project.name}-${project.version}-meta.json")
            json.text = """\
              {
                "version": "${project.version}",
                "date": "${timestamp}",
                "url": "https://github.com/${github_organization}/${project.name}/releases/download/${project.version}/${project.name}-${project.version}.zip",
                "requires": "${metaFromManifest('Plugin-Requires',file('src/resources/META-INF/MANIFEST.MF'))}",
                "sha512sum": "${computeSha512(zip)}"
              }
            """.stripIndent()
            // cleanup tmp dir
            file("$buildDir/tmp/makeZip").deleteDir()
        }
        outputs.file("$buildDir/libs/${project.name}-${project.version}.zip")
    }

    /*
     * Copy the plugin dependencies in the subproject `build/target/libs` directory
     */
    task copyPluginLibs(type: Sync) {
        from configurations.runtimeClasspath
        into 'build/target/libs'
    }

    /*
     * Copy the plugin in the project root build/plugins directory
     */
    task copyPluginZip(type: Copy, dependsOn: project.tasks.findByName('makeZip')) {
        from makeZip
        into "$rootProject.buildDir/plugins"
        outputs.file("$rootProject.buildDir/plugins/${project.name}-${project.version}.zip")
        doLast {
            ant.unzip(
                    src: "$rootProject.buildDir/plugins/${project.name}-${project.version}.zip",
                    dest: "$rootProject.buildDir/plugins/${project.name}-${project.version}"
            )
        }
    }

    /*
    * "install" the plugin the project root build/plugins directory
    */
    project.parent.tasks.getByName("assemble").dependsOn << copyPluginZip

    task uploadPlugin(type: io.nextflow.gradle.tasks.GithubUploader, dependsOn: makeZip) {
        assets = providers.provider {["$buildDir/libs/${project.name}-${project.version}.zip",
                                      "$buildDir/libs/${project.name}-${project.version}-meta.json" ]}
        release = providers.provider { project.version }
        repo = providers.provider { project.name }
        owner = github_organization
        userName = github_username
        authToken = github_access_token
        skipExisting = true
    }

    jar {
        from sourceSets.main.allSource
        doLast {
            file("$buildDir/tmp/jar").deleteDir()
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
    
    task upload(dependsOn: [uploadPlugin] ) { }
}

/*
 * Upload all plugins to the corresponding GitHub repos
 */
task upload(dependsOn: [subprojects.uploadPlugin]) { }

/*
 * Copies the plugins required dependencies in the corresponding lib directory
 */
classes.dependsOn subprojects.copyPluginLibs


/*
 * Merge and publish the plugins index file
 */
task publishIndex( type: io.nextflow.gradle.tasks.GithubRepositoryPublisher ) {
    indexUrl = 'https://github.com/nextflow-io/plugins/main/plugins.json'
    repos = allPlugins()
    owner = github_organization
    githubUser = github_username
    githubEmail = github_commit_email
    githubToken = github_access_token
}
